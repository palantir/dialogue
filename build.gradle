/*
 * (c) Copyright 2018 Palantir Technologies Inc. All rights reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    repositories {
        mavenCentral() { metadataSources { mavenPom(); ignoreGradleMetadataRedirection() } }
        gradlePluginPortal() { metadataSources { mavenPom(); ignoreGradleMetadataRedirection() } }
    }

    dependencies {
        classpath 'com.palantir.jakartapackagealignment:jakarta-package-alignment:0.5.0'
        classpath 'com.palantir.gradle.jdks:gradle-jdks:0.33.0'
        classpath 'com.palantir.gradle.jdkslatest:gradle-jdks-latest:0.11.0'
        classpath 'com.palantir.gradle.externalpublish:gradle-external-publish-plugin:1.12.0'
        classpath 'com.palantir.gradle.revapi:gradle-revapi:1.7.0'
        classpath 'com.palantir.javaformat:gradle-palantir-java-format:2.38.0'
        classpath 'com.palantir.baseline:gradle-baseline-java:5.25.0'
        classpath 'com.palantir.gradle.gitversion:gradle-git-version:3.0.0'
        classpath 'gradle.plugin.org.inferred:gradle-processors:3.7.0'
        classpath 'com.palantir.metricschema:gradle-metric-schema:0.23.0'
        classpath 'com.palantir.gradle.conjure:gradle-conjure:5.41.0'
        classpath 'org.unbroken-dome.gradle-plugins:gradle-testsets-plugin:4.1.0'
    }
}

plugins {
id 'com.palantir.consistent-versions' version '2.16.0'
}

apply plugin: 'com.palantir.external-publish'
apply plugin: 'com.palantir.baseline'
apply plugin: 'com.palantir.git-version'
apply plugin: 'com.palantir.baseline-java-versions'
apply plugin: 'com.palantir.jdks.latest'

javaVersions {
    libraryTarget = 11
    runtime = 21
}

version System.env.CIRCLE_TAG ?: gitVersion()

allprojects {
    apply plugin: 'com.palantir.java-format'
    apply plugin: 'com.palantir.jakarta-package-alignment'

    group 'com.palantir.dialogue'
    version rootProject.version

    repositories {
        mavenCentral() { metadataSources { mavenPom(); ignoreGradleMetadataRedirection() } }
    }
    configurations.all {
        resolutionStrategy.preferProjectModules()
        resolutionStrategy.eachDependency { details ->
            if (details.requested.group == 'com.google.errorprone' && details.requested.name == 'error_prone_annotations') {
                details.useTarget group: 'com.google.errorprone', name: 'error_prone_annotations', version: '2.7.1'
                details.because "The error_prone_annotations dependency must be low to avoid forcing consumers to use newer releases"
            }
        }
    }
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'org.inferred.processors'

    tasks.withType(JavaCompile) {
        options.compilerArgs += ['-Werror']
    }

    plugins.withId('com.palantir.baseline-error-prone', {
        dependencies {
            errorprone 'com.uber.nullaway:nullaway'
        }

        tasks.withType(JavaCompile).configureEach {
            if (name.toLowerCase().contains("test")) {
                options.errorprone.disable('NullAway')
            } else {
                options.errorprone.option('NullAway:AnnotatedPackages', 'com.palantir')
            }
            // This project is performance sensitive
            options.errorprone.disable('PreferJavaTimeOverload')
        }
    })

    tasks.check.dependsOn checkImplicitDependencies
    tasks.checkImplicitDependenciesTest.enabled = false // doesn't impact users
}
