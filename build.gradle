/*
 * (c) Copyright 2018 Palantir Technologies Inc. All rights reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    repositories {
        gradlePluginPortal()
        mavenCentral()
    }

    dependencies {
        classpath 'com.palantir.gradle.externalpublish:gradle-external-publish-plugin:1.8.0'
        classpath 'com.palantir.gradle.revapi:gradle-revapi:1.5.0'
        classpath 'com.palantir.javaformat:gradle-palantir-java-format:2.15.0'
        classpath 'com.palantir.baseline:gradle-baseline-java:4.61.0'
        classpath 'com.palantir.gradle.gitversion:gradle-git-version:0.12.3'
        classpath 'gradle.plugin.org.inferred:gradle-processors:3.6.0'
        classpath 'com.palantir.metricschema:gradle-metric-schema:0.10.0'
        classpath 'com.palantir.gradle.conjure:gradle-conjure:5.19.0'
        classpath 'org.unbroken-dome.gradle-plugins:gradle-testsets-plugin:4.0.0'
    }
}

plugins {
    id 'com.palantir.consistent-versions' version '2.5.0'
}

apply plugin: 'com.palantir.external-publish'
apply plugin: 'com.palantir.baseline'
apply plugin: 'com.palantir.git-version'
apply plugin: 'com.palantir.baseline-java-versions'

javaVersions {
    libraryTarget = 11
    runtime = 17
}

version System.env.CIRCLE_TAG ?: gitVersion()

allprojects {
    apply plugin: 'com.palantir.java-format'

    group 'com.palantir.dialogue'
    version rootProject.version

    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'org.inferred.processors'

    tasks.withType(JavaCompile) {
        options.compilerArgs += ['-Werror']
    }

    plugins.withId('com.palantir.baseline-error-prone', {
        dependencies {
            errorprone 'com.uber.nullaway:nullaway'
        }

        tasks.withType(JavaCompile).configureEach {
            if (name.toLowerCase().contains("test")) {
                options.errorprone.disable('NullAway')
            } else {
                options.errorprone.option('NullAway:AnnotatedPackages', 'com.palantir')
            }
        }
    })

    tasks.check.dependsOn checkImplicitDependencies
    tasks.checkImplicitDependenciesTest.enabled = false // doesn't impact users
}
